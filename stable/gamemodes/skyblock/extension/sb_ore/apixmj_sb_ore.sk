# =====================================================================================================================
# APIxMJ - Files
# ---------------------------------------------------------------------------------------------------------------------
function mj_files_sb_ore():
	set {_folder} to mj_editfiles("manager.data", "data.addon.gamemodes_skyblock_extension.sb_ore.folder")
	load yaml "plugins/APIxMJ/%{_folder}%/config.yml" as "mj.sb_ore.config"
	set {_list::*} to mj_editfiles_list("manager.config", "data.language.list")
	if {_list::*} is set:
		loop {_list::*}:
			load yaml "plugins/APIxMJ/%{_folder}%/language/%loop-value%.yml" as "mj.sb_ore.language.%loop-value%"
			load yaml "plugins/APIxMJ/%{_folder}%/help/%loop-value%.yml" as "mj.sb_ore.help.%loop-value%"
	stop
# ---------------------------------------------------------------------------------------------------------------------
on load:
	set {_list::*} to mj_manager_skript_list()
	if {_list::*} contains "apixmj_manager.sk" and "apixmj_core.sk" and "apixmj_sb_core.sk":
		send "[APIxMJ] Loading sb_ore..." to console
		mj_files_sb_ore()
	else:
		broadcast "[APIxMJ] Script loading cancel It requires (apixmj_manager.sk, apixmj_core.sk and apixmj_sb_core.sk)"
		unload script file "apixmj_sb_ore.sk"
	stop
# ---------------------------------------------------------------------------------------------------------------------
on skript stop:
	rename file "plugins/Skript/scripts/apixmj_sb_ore.sk" to "-apixmj_sb_ore.sk"
	send "[APIxMJ] Unloading sb_ore.."
	stop
# ---------------------------------------------------------------------------------------------------------------------
# APIxMJ - Files
# =====================================================================================================================
# APIxMJ - Command
# ---------------------------------------------------------------------------------------------------------------------
command /ore [<text>] [<text>]:
	executable by: player
	trigger:
		if player has permission "*" or "mj.*" or "mj.ore.*" or "mj.ore.admin":
			if arg-1 is "reload":
				if arg-2 is "files":
					mj_files_sb_ore()
					send "APIxMJ : sb_ore files reloaded"
				else:
					reload script "apixmj_sb_ore"
		else:
			mj_manager_permission(player, "core", "data.prefix", "mj.ore.admin")
		stop
# ---------------------------------------------------------------------------------------------------------------------
on tab complete of "/ore":
	if player has permission "*" or "mj.*" or "mj.ore.*" or "mj.ore.admin":
		set tab completions for position 1 to "reload"
		if tab arg-1 is "reload":
			set tab completions for position 2 to "files"
	stop
# ---------------------------------------------------------------------------------------------------------------------
# APIxMJ - Command
# =====================================================================================================================
# APIxMJ - Function
# ---------------------------------------------------------------------------------------------------------------------
on form:
	if type of event-block is cobblestone block:
		set {_list::*} to mj_manager_nodes("sb_ore.config", "data.default")
		loop {_list::*}:
			set {_pourcent} to mj_editfiles_number("sb_ore.config", "data.default.%loop-value%.pourcent")
			if chance of {_pourcent}%:
				set {_block} to mj_manager_item_convert("sb_ore.config", "data.default.%loop-value%.block")
				set {_check.1} to mj_editfiles_number("sb_core.data", "data.world.%event-world%.rate.level")
				set {_check.2} to mj_editfiles_number("sb_ore.config", "data.default.%loop-value%.request.level")
				if {_check.1} >= {_check.2}:
					wait 1 tick
					set block at event-location to {_block}
					stop
	stop
# ---------------------------------------------------------------------------------------------------------------------
on form:
	if type of event-block is basalt:
		set {_list::*} to mj_manager_nodes("sb_ore.config", "data.nether")
		loop {_list::*}:
			set {_value} to loop-value
			set {_pourcent} to mj_editfiles_number("sb_ore.config", "data.nether.%{_value}%.pourcent")
			if chance of {_pourcent}%:
				set {_block} to mj_manager_item_convert("sb_ore.config", "data.nether.%{_value}%.block")
				set {_check.1} to mj_editfiles_number("sb_core.data", "data.world.%event-world%.rate.level")
				set {_check.2} to mj_editfiles_number("sb_ore.config", "data.nether.%{_value}%.request.level")
				if {_check.1} >= {_check.2}:
					wait 1 tick
					set block at event-location to {_block}
					stop
	stop
# ---------------------------------------------------------------------------------------------------------------------
# APIxMJ - Function
# =====================================================================================================================


