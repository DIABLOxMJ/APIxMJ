# =====================================================================================================================
# APIxMJ - Files
# ---------------------------------------------------------------------------------------------------------------------
function mj_files_rankup():
	set {_folder} to mj_editfiles("manager.data", "data.addon.vertebral_extension.rankup.folder")
	load yaml "plugins/APIxMJ/%{_folder}%/config.yml" as "mj.rankup.config"
	load yaml "plugins/APIxMJ/%{_folder}%/data.yml" as "mj.rankup.data"
	set {_list::*} to mj_editfiles_list("manager.config", "data.language.list")
	if {_list::*} is set:
		loop {_list::*}:
			load yaml "plugins/APIxMJ/%{_folder}%/language/%loop-value%.yml" as "mj.rankup.language.%loop-value%"
			load yaml "plugins/APIxMJ/%{_folder}%/help/%loop-value%.yml" as "mj.rankup.help.%loop-value%"
	stop
# ---------------------------------------------------------------------------------------------------------------------
on load:
	set {_list::*} to mj_manager_skript_list()
	if {_list::*} contains "apixmj_manager.sk" and "apixmj.sk" and "apixmj_ranker.sk" and "apixmj_essentials.sk":
		send "[APIxMJ] Loading rankup..." to console
		mj_files_rankup()
		mj_editfiles("manager.data", "data.addon.vertebral_extension.rankup.version", "modify", "2023.05.05.0001")
	else:
		broadcast "[APIxMJ] Script loading cancel It requires (apixmj_manager.sk, apixmj.sk, apixmj_ranker.sk and apixmj_essentials.sk)"
		unload script file "apixmj_rankup.sk"
	stop
# ---------------------------------------------------------------------------------------------------------------------
on skript stop:
	rename file "plugins/Skript/scripts/apixmj_rankup.sk" to "-apixmj_rankup.sk"
	send "[APIxMJ] Unloading rankup.."
	stop
# ---------------------------------------------------------------------------------------------------------------------
# APIxMJ - Files
# =====================================================================================================================
# APIxMJ - Command
# ---------------------------------------------------------------------------------------------------------------------
command /rankup:
	executable by: player
	trigger:
		if player has permission "*" or "mj.*" or "mj.rankup.*" or "mj.rankup":
			mj_rankup_menu_main(player)
		else:
			mj_manager_permission(player, "rankup", "data.prefix", "mj.rankup")
		stop
# ---------------------------------------------------------------------------------------------------------------------
# APIxMJ - Command
# =====================================================================================================================
# APIxMJ - Main
# ---------------------------------------------------------------------------------------------------------------------
function mj_rankup_menu_main(player: player):
	set {_name} to mj_editfiles_gettext("rankup", "data.title", {_player})
	set {_size} to mj_editfiles_number("rankup.config", "data.gui_size")
	mj_core_gui_generator({_player}, {_size}, true, false, {_name}, "rankup")
	set {_list::*} to mj_manager_nodes("rankup.data")
	loop {_list::*}:
		set {_value} to loop-value
		set {_check} to mj_editfiles_boolean("rankup.data", "data.%{_value}%.visible")
		if {_check} is true:
			mj_rankup_menu_main_button_choice({_player}, {_value})
	stop
# ---------------------------------------------------------------------------------------------------------------------
function mj_rankup_menu_main_button_choice(player: player, id: text):
	set {_lang} to mj_editfiles("manager.config", "data.language.use")
	set {_find_name} to mj_editfiles("rankup.data", "data.%{_id}%.name")
	set {_name} to mj_editfiles_gettext("rankup", {_find_name}, {_player})
	#set {_title} to mj_editfiles_gettext("rankup", "data.item_name", {_player}, {_name})
	set {_slot} to mj_editfiles_number("rankup.data", "data.%{_id}%.slot")
	set {_item1} to mj_rankup_menu_main_button_choice_info({_player}, {_id})
	set {_price} to mj_editfiles_number("rankup.data", "data.%{_id}%.price")
	set {_money} to mj_essentials_money_modify({_player}, "all")
	set {_rank} to mj_ranker({_player}, "source")
	set {_request} to mj_editfiles("rankup.data", "data.%{_id}%.request")
	set {_new_rank} to mj_editfiles("rankup.data", "data.%{_id}%.rank")
	set {_buy} to false
	if {_rank} is {_new_rank}:
		set {_item2} to mj_manager_item_button({_player}, {_item1}, "left_click", "rankup", "data.click_rank_acquired")
	else if {_rank} is {_request}:
		if {_money} >= {_price}:
			set {_item2} to mj_manager_item_button({_player}, {_item1}, "left_click", "rankup", "data.click_buy")
			set {_buy} to true
		else:
			set {_item2} to mj_manager_item_button({_player}, {_item1}, "left_click", "rankup", "data.click_money_request")
	else:
		set {_item2} to mj_manager_item_button({_player}, {_item1}, "left_click", "rankup", "data.click_rank_request")
	make a gui slot {_slot} of {_player} with {_item2} named {_name} to run:
		if {_buy} is true:
			mj_rankup_menu_confirm({_player}, {_id})
		stop
	stop
# ---------------------------------------------------------------------------------------------------------------------
function mj_rankup_menu_main_button_choice_info(player: player, id: text) :: item:
	set {_lang} to mj_editfiles("manager.config", "data.language.use")
	set {_request} to mj_editfiles("rankup.data", "data.%{_id}%.request")
	set {_rank} to mj_editfiles("rankup.data", "data.%{_id}%.rank")
	set {_price} to mj_editfiles_number("rankup.data", "data.%{_id}%.price")
	set {_item} to mj_manager_item_convert("rankup.data", "data.%{_id}%.item")
	set {_view1} to mj_ranker_convert({_request})
	set {_view2} to mj_ranker_convert({_rank})
	set {_view3} to mj_core_format_money(false, {_price})
	set {_view4} to mj_editfiles("rankup.data", "data.%{_id}%.web_price")
	set {_item1} to mj_core_item_lore({_player}, {_item}, 2, "rankup", "data.item_request", {_view1})
	set {_item2} to mj_core_item_lore({_player}, {_item1}, 3, "rankup", "data.item_rank", {_view2})
	set {_item3} to mj_core_item_lore({_player}, {_item2}, 5, "rankup", "data.item_price", {_view3})
	set {_item4} to mj_core_item_lore({_player}, {_item3}, 6, "rankup", "data.item_web_price", {_view4})
	set {_item5} to mj_core_item_lore({_player}, {_item4}, 8, "rankup", "data.functionality")
	set {_count} to mj_manager_item_lore_count({_player}, {_item5})
	set {_list::*} to mj_manager_nodes("rankup.language.%{_lang}%", "data.product.%{_id}%.info")
	loop {_list::*}:
		set {_info} to mj_editfiles_gettext("rankup", "data.product.%{_id}%.info.%loop-value%")
		set line {_count} of lore of {_item5} to colored {_info}
		add 1 to {_count}
	return {_item5}
# ---------------------------------------------------------------------------------------------------------------------
# APIxMJ - Main
# =====================================================================================================================
# APIxMJ - Confirm
# ---------------------------------------------------------------------------------------------------------------------
function mj_rankup_menu_confirm(player: player, id: text):
	set {_name} to mj_editfiles_gettext("rankup", "data.title_confirm", {_player})
	mj_core_gui_generator({_player}, 3, true, false, {_name}, "rankup")
	mj_rankup_menu_confirm_button_buy({_player}, {_id}, 11)
	mj_rankup_menu_confirm_button_store({_player}, {_id}, 15)
	stop
# ---------------------------------------------------------------------------------------------------------------------
function mj_rankup_menu_confirm_button_buy(player: player, id: text, slot: number):
	set {_rank1} to mj_editfiles("rankup.data", "data.%{_id}%.rank")
	set {_rank2} to mj_ranker_convert({_rank1})
	set {_web_price} to mj_editfiles("rankup.data", "data.%{_id}%.web_price")
	set {_price1} to mj_editfiles_number("rankup.data", "data.%{_id}%.price")
	set {_price2} to mj_core_format_money(false, {_price1})
	set {_item} to mj_core_item_lore({_player}, lime stained glass pane, 2, "rankup", "data.item_price", {_price2})
	set {_title} to mj_editfiles_gettext("rankup", "data.buy_confirm", {_player}, {_rank2})
	set {_item1} to mj_manager_item_button({_player}, {_item}, "left_click", "rankup", "data.click_buy_confirm")
	make a gui slot {_slot} of {_player} with {_item1} named {_title} to close:
		mj_essentials_money_system({_player}, {_price1})
		mj_ranker_modify({_player}, {_rank1})
		loop all players:
			mj_manager_message(loop-player, "rankup", "data.prefix", "rankup", "data.success", "%{_player}%", {_rank2})
		stop
	stop
# ---------------------------------------------------------------------------------------------------------------------
function mj_rankup_menu_confirm_button_store(player: player, id: text, slot: number):
	set {_price} to mj_editfiles("rankup.data", "data.%{_id}%.web_price")
	set {_link} to mj_editfiles("rankup.config", "data.website_store")
	set {_item} to mj_core_item_lore({_player}, green stained glass pane, 2, "rankup", "data.item_web_price", {_price})
	set {_text} to mj_editfiles_gettext("rankup", "data.store")
	set {_item1} to mj_manager_item_button({_player}, {_item}, "left_click", "rankup", "data.click_open_store")
	make a gui slot {_slot} of {_player} with {_item1} named {_text} to close:
		mj_manager_message({_player}, "rankup", "data.prefix", "rankup", "data.link_website_store", {_link})
		stop
	stop
# ---------------------------------------------------------------------------------------------------------------------
# APIxMJ - Confirm
# =====================================================================================================================